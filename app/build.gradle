apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: '../gradle/configurations.gradle'
apply from: '../gradle/constant.gradle'

android {
  compileSdkVersion 28

  defaultConfig {
    applicationId "com.mastercard.fancyshop"
    minSdkVersion 19
    targetSdkVersion 28
    versionCode 1
    versionName "1.0"
    multiDexEnabled true
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  publishNonDefault true

  buildTypes {
    debug {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  flavorDimensions flavor_dimension

  productFlavors {
    environments.each { env ->
      "$env.flavour" {
        buildConfigField STRING, ENVIRONMENT, "\"$env.name\""
        buildConfigField STRING, CHECKOUTID, "\"$env.checkoutId\""
        buildConfigField STRING, PASSWORD, "\"$env.password\""
        buildConfigField STRING, MERCHANTP12CERTIFICATE, "\"$env.merchantP12Certificate\""
        buildConfigField STRING, KEYALIAS, "\"$env.keyAlias\""
        buildConfigField STRING, CLIENTID, "\"$env.clientId\""
        buildConfigField STRING, CHECKOUT_URL, "\"$env.checkoutUrl\""
      }
    }
  }

  lintOptions {
    abortOnError false
  }
}

repositories {
  flatDir {
    dirs 'libs'
  }
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])

  api project(':commerce')

  implementation 'com.google.code.gson:gson:2.8.2'
  implementation supportDependencies.appCompat
  implementation 'com.android.support:design:28.0.0'
  implementation supportDependencies.constraintLayout
  implementation 'com.squareup.okhttp3:okhttp:3.9.0'
  implementation 'com.github.bumptech.glide:glide:4.2.0'

  androidTestImplementation fileTree(include: ['spoon-client-1.6.4.jar'],
      dir: 'src/androidTest/spoon')
  androidTestImplementation('com.android.support.test.espresso:espresso-core:3.0.0', {
    exclude group: 'com.android.support', module: 'support-annotations'
  })
  androidTestImplementation('com.android.support.test:runner:1.0.2') {
    exclude module: 'support-annotations'
  }
  androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.2'
  // Set this dependency to use JUnit 4 rules
  androidTestImplementation('com.android.support.test:rules:1.0.2') {
    exclude module: 'support-annotations'
  }
  androidTestImplementation('tools.fastlane:screengrab:1.1.0') {
    exclude module: 'support-annotations'
  }
  androidTestUtil 'com.android.support.test:orchestrator:1.0.2'
  androidTestImplementation 'com.android.support.test.uiautomator:uiautomator-v18:2.1.3'
  androidTestImplementation 'com.mastercard.rally:rally-update-android:0.0.15'
  androidTestImplementation('tools.fastlane:screengrab:1.1.0') {
    exclude module: 'support-annotations'
  }
  testImplementation 'junit:junit:4.12'
  testImplementation 'org.mockito:mockito-core:1.10.19'
  testImplementation testDependencies.powerMockitoJUnit
  testImplementation testDependencies.powermockMockito
  testImplementation testDependencies.powermockMockitoCommon
  testImplementation 'com.google.android:android-test:4.1.1.4'


}
